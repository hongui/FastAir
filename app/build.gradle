plugins{
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id("com.google.devtools.ksp") version "1.8.0-1.0.8"
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.mob.lee.fastair"
        minSdk 21
        targetSdk 33
        versionCode 10
        versionName "1.6.0"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/*']
        }
        resources {
            excludes += ['META-INF/*']
        }
    }

    namespace 'com.mob.lee.fastair'
}

task clearAssets(type: Delete){
    def path="${projectDir}/src/main/assets"
    def target=fileTree(dir: path,exclude:['about.html','help.html'])
    delete(target,"$path/app")
}

task frontEnd(type:Exec){
    dependsOn(clearAssets)
    workingDir("${rootDir}/FastSvelte")
    commandLine('npm.cmd','run','build')
}

task toAssets(type: Copy){
    def path="${projectDir}/src/main/assets"
    dependsOn(frontEnd)
    from("${rootDir}/FastSvelte/build")
    into(path)
    doLast {
        file("$path/_app").renameTo("$path/app")
    }
}

dependencies {
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.5.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.room:room-runtime:2.5.0'
    implementation 'androidx.room:room-ktx:2.5.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    ksp 'androidx.room:room-compiler:2.5.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"
}
