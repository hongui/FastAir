import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion sdk.compile
    buildToolsVersion buildTool
    defaultConfig {
        applicationId "com.mob.lee.fastair"
        minSdkVersion sdk.mins
        targetSdkVersion sdk.target
        versionCode appVersion.code
        versionName appVersion.name
        multiDexEnabled true
    }

    signingConfigs{
        release{
            Properties properties = new Properties()
            InputStream stream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load(stream)

            storeFile file(properties.getProperty('keystore.path'))
            storePassword properties.getProperty('keystore.password')
            keyAlias properties.getProperty('alias.name')
            keyPassword properties.getProperty('alias.password')
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all{
        variant->
            if("release"!=variant.getName()){
                return
            }
            variant.outputs.all{
                output->
                    def name="${variant.applicationId}_${variant.getVersionName()}_${new SimpleDateFormat('yyyy_MM_dd_HH_mm').format(System.currentTimeMillis())}.apk"
                    outputFileName=name
            }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        pickFirst 'META-INF/*'
    }
}

dependencies {
    dependencyList.activity
    implementation dependencyList.activity
    implementation dependencyList.cardview
    implementation dependencyList.constraintlayout
    implementation dependencyList.fragment
    implementation dependencyList.kdt
    implementation dependencyList.kotlincoroutine
    implementation dependencyList.kotlinreflect
    implementation dependencyList.lifecycle
    implementation dependencyList.lifecycleruntime
    implementation dependencyList.material
    implementation dependencyList.multidex
    implementation dependencyList.navigation
    implementation dependencyList.navigationui
    implementation dependencyList.paging
    implementation dependencyList.preference
    implementation dependencyList.recyclerview
    implementation dependencyList.room
    implementation dependencyList.roomkapt
    implementation dependencyList.roomextension
    implementation dependencyList.viewpager

    kapt dependencyList.roomkapt
}
