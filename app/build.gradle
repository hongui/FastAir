import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion sdk.compile
    defaultConfig {
        applicationId "com.mob.lee.fastair"
        minSdkVersion sdk.mins
        targetSdkVersion sdk.target
        versionCode appVersion.code
        versionName appVersion.name
        multiDexEnabled true
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            InputStream stream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load(stream)

            storeFile file(properties.getProperty('keystore.path'))
            storePassword properties.getProperty('keystore.password')
            keyAlias properties.getProperty('alias.name')
            keyPassword properties.getProperty('alias.password')
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all {
        variant ->
            if ("release" != variant.getName()) {
                return
            }
            variant.outputs.all {
                output ->
                    def name = "${variant.applicationId}_${variant.getVersionName()}_${new SimpleDateFormat('yyyy_MM_dd_HH_mm').format(System.currentTimeMillis())}.apk"
                    outputFileName = name
            }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation 'androidx.activity:activity-ktx:1.2.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.3'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.3.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.paging:paging-runtime:3.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    kapt 'androidx.room:room-compiler:2.3.0'
}
